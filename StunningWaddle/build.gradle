apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

android {

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
    }

    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs '../libs'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/titanium-knights/stunning-waddle")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_PASSWORD")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId 'io.github.titanium_knights'
            artifactId 'stunning_waddle'
            version '0.1'
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/StunningWaddle-release.aar")
        }
    }
}

apply from: 'build.release.gradle'
